package sbnz.rules;

import ftn.sbnz.model.company.Company;
import ftn.sbnz.model.company.CompanyReview;
import ftn.sbnz.model.enums.ReviewStatus;

import ftn.sbnz.events.ReviewDeclinedEvent;
import ftn.sbnz.events.MaliciousReviewerEvent;
import ftn.sbnz.events.DisabledUserEvent;


rule "Review declined - ReviewDeclinedEvent added"
    agenda-group "company-review-added"
    when
        $companyReview: CompanyReview (status == ReviewStatus.DECLINED, $user: poster, $date: date.getTime()) and
        Number(intValue == 0) from accumulate 
        					  	   (MaliciousReviewerEvent($user.id == jobSeekerId, $date before endDate), count(1))
    then
        System.out.println("Review from user with username " + $user.getUsername() + " review declined! New ReviewDeclinedEvent added.");
        ReviewDeclinedEvent event = new ReviewDeclinedEvent($user.getId());
        insert(event);
        delete($companyReview);
end

rule "Review declined - MaliciousReviewer in progress, no ReviewDeclineEvent added"
    agenda-group "company-review-added"
    when
        $companyReview: CompanyReview (status == ReviewStatus.DECLINED, $user: poster, $date: date.getTime()) and
        Number(intValue >= 1) from accumulate 
        					  	   (MaliciousReviewerEvent ($user.id == jobSeekerId, $date before endDate), count(1))
    then
        System.out.println("Review from user with username " + $user.getUsername() + " review declined! No ReviewDeclinedEvent, active MaliciousUser.");
        delete($companyReview);
end

rule "MaliciousReviewerEvent added - 3 reviews declined in 10 days"
    agenda-group "company-review-added"
    when
        $user: JobSeeker($id: id) and
        $eventCounter: Number(intValue > 2) from accumulate (ReviewDeclinedEvent ($jobSeekerId: jobSeekerId, $jobSeekerId == $id) over window:time( 5d ), count(1))
    then
        System.out.println($eventCounter + " ReviewDeclinedEvents from user with username " + $user.getUsername() + "! New MaliciousReviewerEvent added.");
        MaliciousReviewerEvent event = new MaliciousReviewerEvent($user.getId());
        insert(event);
end

rule "MaliciousReviewerEvent added - delete all ReviewDeclinedEvents before it"
    agenda-group "company-review-added"
    when
        $user: JobSeeker($id: id) and
        $mre: MaliciousReviewerEvent(jobSeekerId == $id, $startDate: startDate) and
        $rde: ReviewDeclinedEvent (jobSeekerId == $id, $date: date, this before $startDate)
    then
        System.out.println("ReviewDeclinedEvent removed because of new MaliciousReviewerEvent.");
        delete($rde);
end

rule "Disable user - MaliciousReviewerEvent happened 2 times in last 90 days"
    agenda-group "company-review-added"
    when
        $user: JobSeeker($id: id) and
        $eventCounter: Number(intValue > 1) from accumulate (MaliciousReviewerEvent ($jobSeekerId: jobSeekerId, $jobSeekerId == $id) over window:time( 90d ), count(1))
    then
        System.out.println($eventCounter + " MaliciousReviewerEvents from user with username " + $user.getUsername() + "! User suspended for 120 days!");
        DisabledUserEvent event = new DisabledUserEvent($user.getId());
        insert(event);
end