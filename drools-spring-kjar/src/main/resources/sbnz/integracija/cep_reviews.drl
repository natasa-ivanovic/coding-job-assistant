package sbnz.integracija;

import ftn.sbnz.model.company.Company;
import ftn.sbnz.model.job_offer.JobOfferReview;
import ftn.sbnz.model.enums.MedalRank;
import ftn.sbnz.model.enums.ReviewStatus;

import ftn.sbnz.events.ReviewDeclinedEvent;
import ftn.sbnz.events.MaliciousReviewerEvent;
import ftn.sbnz.events.DisabledUserEvent;
import ftn.sbnz.events.UserAccountStatusEvent;

import java.time.LocalDate;
import java.time.LocalDateTime;
import java.sql.Timestamp;
import java.util.List;
import java.util.Set;


rule "ReviewDeclinedEvent added - review declined"
    agenda-group "job-offer-review-added"
    when
        $jobOfferReview: JobOfferReview (status == ReviewStatus.DECLINED, $user: poster)
    then
        System.out.println("Review from user with username " + $user.getUsername() + " review declined! New ReviewDeclinedEvent added.");
        ReviewDeclinedEvent event = new ReviewDeclinedEvent($user.getId());
        insert(event);
        delete($jobOfferReview);
end

rule "MaliciousReviewerEvent added - 3 reviews declined in 10 days"
    agenda-group "job-offer-review-added"
    when
        $user: JobSeeker($id: id) and
        $eventCounter: Number(intValue > 2) from accumulate (ReviewDeclinedEvent ($jobSeekerId: jobSeekerId, $jobSeekerId == $id) over window:time( 15s ), count(1))
    then
        System.out.println($eventCounter + " ReviewDeclinedEvents from user with username " + $user.getUsername() + "! New MaliciousReviewerEvent added.");
        MaliciousReviewerEvent event = new MaliciousReviewerEvent($user.getId());
        insert(event);
end

rule "Disable user - MaliciousReviewerEvent happened 3 times in last 90 days"
    agenda-group "job-offer-review-added"
    when
        $user: JobSeeker($id: id) and
        $eventCounter: Number(intValue > 2) from accumulate (MaliciousReviewerEvent ($jobSeekerId: jobSeekerId, $jobSeekerId == $id) over window:time( 90d ), count(1))
    then
        System.out.println($eventCounter + " MaliciousReviewerEvents from user with username " + $user.getUsername() + "! User suspended for 120 days!");
        DisabledUserEvent event = new DisabledUserEvent($user.getId());
        insert(event);
end